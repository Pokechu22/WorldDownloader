buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = 'sponge'
            url = 'http://repo.spongepowered.org/maven'
        }
        jcenter()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'commons-io:commons-io:2.5'
        classpath 'com.googlecode.java-diff-utils:diffutils:1.3.0'
        classpath 'pokechu22.test.begradle:begradle:0.1.0@jar'
        classpath 'org.spongepowered:mixingradle:0.5-SNAPSHOT'
    }
}

apply plugin: 'java'

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:2.8.47'
}

version = "4.0.1.0-SNAPSHOT"
group = "wdl"
archivesBaseName = "LiteModWDL"
libsDirName = "../../libs/${version}"

apply plugin: 'net.minecraftforge.gradle.liteloader'
apply plugin: 'pokechu22.test.customsrg'
apply plugin: 'org.spongepowered.mixin'

minecraft {
    version = "1.12"
    mappings = "snapshot_20170801"
    runDir = "run"

    // We don't currently want a source jar (doesn't work well for base edits, and the version info doesn't work yet)
    makeObfSourceJar = false
}

extraSrgs {
    add file('../srgs/AbstractHorseToEquineEntity.srg')
}

sourceSets {
    main {
        java {
            srcDir "src/main/java"
            srcDir project(':1.12').file("src/main/java")
            srcDir project(':share').file("src/main/java")
        }
        resources {
            srcDir "src/main/resources"
            srcDir project(':share').file("src/main/resources")
        }
        // Refmap declaration must match the refmap name specified in the json config
        ext.refMap = "mixins.LiteModWDL.refmap.json"
    }
    test {
        java {
            srcDir project(':1.12').file("src/test/java")
            srcDir project(':share').file("src/test/java")
        }
    }
    api {
        java {
            srcDir project(':share').file("src/api/java")
        }
    }
}

mixin {
    defaultObfuscationEnv notch
}

litemod {
    json {
        name = "LiteModWDL"
        displayName = "World Downloader for Liteloader"
        author = "uyjulian, pokechu22"
        mcversion = "1.12"
        mixinConfigs += "mixins.LiteModWDL.json"
        version = project.version
    }
}

// We want Java 8 where possible
tasks.withType(JavaCompile) { javaTask ->
    javaTask.sourceCompatibility = "1.8"
    javaTask.targetCompatibility = "1.8"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

// Use tabs instead of spaces
fixMcSources {
    astyleConfig = "../astyle.cfg"
}

// Ensure that the API code and litemod JSON in the jar
jar {
    from sourceSets.api.output
    from litemod.outputs
}
