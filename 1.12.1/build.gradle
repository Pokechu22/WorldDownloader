buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        jcenter()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'commons-io:commons-io:2.5'
        classpath 'com.googlecode.java-diff-utils:diffutils:1.3.0'
        classpath 'pokechu22.test.begradle:begradle:0.1.3@jar'
    }
}

apply plugin: 'java'

version = "4.0.2.1-SNAPSHOT"
group = "wdl"
archivesBaseName = "WorldDownloader"
libsDirName = "../../libs/${version}"

apply plugin: 'pokechu22.test.begradle'
apply plugin: 'pokechu22.test.customsrg'
apply plugin: 'pokechu22.test.noinplacereobf'

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:2.8.47'
    // Make sure that we have the Minecraft deps, at the correct versions
    testCompile configurations.forgeGradleMcDeps
}

minecraft {
    suppressMappingVersionWarnings = true
    version = "1.12.1"
    mappings = "snapshot_20180605"
    runDir = "run"

    // We don't currently want a source jar (doesn't work well for base edits, and the version info doesn't work yet)
    makeObfSourceJar = false
}

extraSrgs {
    addSrg file('../srgs/AbstractHorseToEquineEntity.srg')
}

sourceSets {
    main {
        java {
            srcDir "src/main/java"
            srcDir project(':share').file("src/main/java")
        }
        resources {
            srcDir project(':share').file("src/main/resources")
        }
        base {
            baseClasses = [
                // Main hooks
                "net.minecraft.client.gui.GuiIngameMenu",
                "net.minecraft.client.multiplayer.WorldClient",
                "net.minecraft.client.network.NetHandlerPlayClient",
                "net.minecraft.inventory.InventoryBasic",
                // Auxiliary hooks, only needed for the base edit
                "net.minecraft.crash.CrashReport",
                "net.minecraft.client.ClientBrandRetriever",
                "net.minecraft.client.resources.DefaultResourcePack"
            ]
        }
    }
    test {
        java {
            srcDir "src/test/java"
            srcDir project(':share').file("src/test/java")
        }
    }
    api {
        java {
            srcDir project(':share').file("src/api/java")
        }
    }
}

// We want Java 8 where possible
tasks.withType(JavaCompile) { javaTask ->
    javaTask.sourceCompatibility = "1.8"
    javaTask.targetCompatibility = "1.8"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

// Use tabs instead of spaces
fixMcSources {
    astyleConfig = "../astyle.cfg"
}

// Ensure that the API code is in the jar
jar {
    from sourceSets.api.output
}
