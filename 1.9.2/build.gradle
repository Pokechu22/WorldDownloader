buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        jcenter()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'commons-io:commons-io:2.5'
        classpath 'com.googlecode.java-diff-utils:diffutils:1.3.0'
        classpath 'pokechu22.test.begradle:begradle:0.1.3-SNAPSHOT@jar'
    }
}

apply plugin: 'java'

configurations {
    shade
    compile.extendsFrom(shade)
}

dependencies {
    shade 'it.unimi.dsi:fastutil:7.0.12_mojang'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:2.8.47'
}

version = "4.0.1.8-SNAPSHOT"
group = "wdl"
archivesBaseName = "WorldDownloader"
libsDirName = "../../libs/${version}"

apply plugin: 'pokechu22.test.begradle'
apply plugin: 'pokechu22.test.customsrg'
apply plugin: 'pokechu22.test.langrenamer'
apply plugin: 'pokechu22.test.noinplacereobf'

minecraft {
    suppressMappingVersionWarnings = true
    // 1.9.2 actually does work with FG 2.2, but it'll claim it doesn't
    suppressVersionTest = true
    version = "1.9.2"
    mappings = "snapshot_20180120"
    runDir = "run"

    // We don't currently want a source jar (doesn't work well for base edits, and the version info doesn't work yet)
    makeObfSourceJar = false
    // en_us.lang to en_US.lang
    ext.renameLangFiles = true
}

extraSrgs {
    addSrg file('../srgs/EntityHorseToEquineEntity.srg')
    addSrg file('../srgs/AnimalChestToContainerHorseChest.srg')
    addSrg file('../srgs/HorseArmorTypeSwap.srg')
    addSrg file('../srgs/ChunkCoordIntPairToChunkPos.srg')
    addMethods file('../srgs/liteloader_1.9_methods.csv')
    addMethods file('../srgs/1.9.2_methods.csv')
    addMethods file('../srgs/old_inventory_methods.csv')
    addFields file('../srgs/1.9.2_fields.csv')
    addFields file('../srgs/old_inventory_fields.csv')
    addPatch('net.minecraft.entity.passive.EquineEntity.java', file('../srgs/EE_1.9_1.9.2.patch'))
    addPatch('net.minecraft.server.management.PlayerManager.java', file('../srgs/PlayerManager_1.9.patch'))
}

sourceSets {
    main {
        java {
            srcDir "src/main/java"
            srcDir project(':share').file("src/main/java")
            exclude "wdl/handler/block/package-info.java"
            exclude "wdl/handler/block/ShulkerBoxHandler.java"
        }
        resources {
            srcDir project(':share').file("src/main/resources")
        }
        base {
            baseClasses = [
                // Main hooks
                "net.minecraft.client.gui.GuiIngameMenu",
                "net.minecraft.client.multiplayer.WorldClient",
                "net.minecraft.client.network.NetHandlerPlayClient",
                // Auxiliary hooks, only needed for the base edit
                "net.minecraft.crash.CrashReport",
                "net.minecraft.client.ClientBrandRetriever",
                "net.minecraft.client.resources.DefaultResourcePack"
            ]
        }
    }
    test {
        java {
            srcDir "src/test/java"
            srcDir project(':share').file("src/test/java")
            exclude "wdl/handler/block/ShulkerBoxTest.java"
        }
    }
    api {
        java {
            srcDir project(':share').file("src/api/java")
        }
    }
}

// We want Java 8 where possible
tasks.withType(JavaCompile) { javaTask ->
    javaTask.sourceCompatibility = "1.8"
    javaTask.targetCompatibility = "1.8"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

// Use tabs instead of spaces
fixMcSources {
    astyleConfig = "../astyle.cfg"
}

// Ensure that the API code is in the jar
jar {
    from sourceSets.api.output
    configurations.shade.each {
        from(zipTree(it)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
}
