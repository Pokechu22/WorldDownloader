buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        jcenter()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.0.154'
        classpath 'pokechu22.test.begradle:begradle:0.2.3-SNAPSHOT:fg3'
    }
}

apply plugin: 'java'

version = "4.0.6.1-SNAPSHOT"
group = "wdl"
archivesBaseName = "WorldDownloader"
libsDirName = "../../libs/${version}"


apply plugin: 'pokechu22.test.begradle'
//apply plugin: 'pokechu22.test.customsrg'
apply plugin: 'pokechu22.test.langrenamer'
//apply plugin: 'pokechu22.test.noinplacereobf'

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:2.22.0'
    // Make sure that we have the Minecraft deps, at the correct versions
    // testCompile configurations.forgeGradleMcDeps
}

sourceSets {
    main
    test
    api
}

minecraft {
    //suppressMappingVersionWarnings = true
    mappings channel: 'snapshot', version: '20200122-1.15.1'
    runs {
        client {
            workingDirectory project.file('run')
            main 'mcp.client.Start'
        }
    }

    // We don't currently want a source jar (doesn't work well for base edits, and the version info doesn't work yet)
    //makeObfSourceJar = false
    // 1.13's json language files
    ext.jsonLangFiles = true
    // Copy one language to its variants
    ext.langMap = [
        'es_es': ['es_ar', 'es_cl', 'es_mx', 'es_uy', 'es_ve'],
        'fr_fr': ['fr_ca']
    ]
}

configurations {
    apiImplementation.extendsFrom implementation
}

dependencies {
    minecraft 'net.minecraft:client:1.15.1'
    // HACK: For some reason the libraries MC depends upon aren't added automatically
    implementation "com.mojang:patchy:1.1"
    implementation "oshi-project:oshi-core:1.1"
    implementation "net.java.dev.jna:jna:4.4.0"
    implementation "net.java.dev.jna:platform:3.4.0"
    implementation "com.ibm.icu:icu4j-core-mojang:51.2"
    implementation "com.mojang:javabridge:1.0.22"
    implementation "net.sf.jopt-simple:jopt-simple:5.0.3"
    implementation "io.netty:netty-all:4.1.25.Final"
    implementation "com.google.guava:guava:21.0"
    implementation "org.apache.commons:commons-lang3:3.5"
    implementation "commons-io:commons-io:2.5"
    implementation "commons-codec:commons-codec:1.10"
    implementation "net.java.jinput:jinput:2.0.5"
    implementation "net.java.jutils:jutils:1.0.0"
    implementation "com.mojang:brigadier:1.0.17"
    implementation "com.mojang:datafixerupper:2.0.24"
    implementation "com.google.code.gson:gson:2.8.0"
    implementation "com.mojang:authlib:1.5.25"
    implementation "org.apache.commons:commons-compress:1.8.1"
    implementation "org.apache.httpcomponents:httpclient:4.3.3"
    implementation "commons-logging:commons-logging:1.1.3"
    implementation "org.apache.httpcomponents:httpcore:4.3.2"
    implementation "it.unimi.dsi:fastutil:8.2.1"
    implementation "org.apache.logging.log4j:log4j-api:2.8.1"
    implementation "org.apache.logging.log4j:log4j-core:2.8.1"
    implementation "org.lwjgl:lwjgl:3.2.2"
    implementation "org.lwjgl:lwjgl-jemalloc:3.2.2"
    implementation "org.lwjgl:lwjgl-openal:3.2.2"
    implementation "org.lwjgl:lwjgl-opengl:3.2.2"
    implementation "org.lwjgl:lwjgl-glfw:3.2.2"
    implementation "org.lwjgl:lwjgl-stb:3.2.2"
    implementation "org.lwjgl:lwjgl-tinyfd:3.2.2"
    implementation "com.mojang:text2speech:1.11.3"
    implementation "ca.weblite:java-objc-bridge:1.0.0"
    implementation "com.google.code.findbugs:jsr305:3.0.1"
    // HACK: Targetting server data so that it's not clobbered for matching the client code
    // The data is the same, but it's still jank to use it instead of the client data (or it being added automatically)
    compile 'net.minecraft:server:1.15.1:data'
}

sourceSets {
    main {
        java {
            compileClasspath += api.output
            runtimeClasspath += api.output
            srcDir "src/main/java"
            srcDir project(':share_14').file("src/main/java")
            exclude "wdl/WDLChunkLoader_1_14.java"
            exclude "wdl/versioned/GameRules_1_14.java"
            exclude "wdl/versioned/Handlers_1_14.java"
            exclude "wdl/versioned/Gui_1_14.java"
            exclude "wdl/versioned/NBT_1_14.java"
        }
        resources {
            srcDir project(':share').file("src/main/resources")
        }
        base {
            baseClasses = [
                // Main hooks
                "net.minecraft.client.gui.screen.IngameMenuScreen",
                "net.minecraft.client.world.ClientWorld",
                "net.minecraft.client.network.play.ClientPlayNetHandler",
                "net.minecraft.inventory.Inventory",
                // Auxiliary hooks, only needed for the base edit
                "net.minecraft.crash.CrashReport",
                "net.minecraft.client.ClientBrandRetriever",
                "net.minecraft.client.resources.VirtualAssetsPack"
            ]
        }
    }
    test {
        java {
            compileClasspath += api.output
            runtimeClasspath += api.output
            srcDir project(':share_14').file("src/test/java")
            exclude "wdl/DelegateChunkManager_1_14.java"
            exclude "wdl/DelegateChunkManager_1_14_3.java"
            exclude "wdl/ExtWorld_1_14.java"
            exclude "wdl/ExtWorld_1_14_4.java"
            exclude "wdl/lwts/WDLTestTweaker_1_9.java"
            exclude "wdl/lwts/WDLTestTweaker_1_10.java"
            exclude "wdl/lwts/WDLTestTweaker.java"
        }
    }
    api {
        java {
            srcDir project(':share_14').file("src/api/java")
        }
    }
}
// We want Java 8 where possible
tasks.withType(JavaCompile) { javaTask ->
    javaTask.sourceCompatibility = "1.8"
    javaTask.targetCompatibility = "1.8"
    javaTask.dependsOn("processBasePatches")
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

// Use tabs instead of spaces
/*fixMcSources {
    astyleConfig = "../astyle.cfg"
}*/

// Ensure that the API code is in the jar
jar {
    from sourceSets.api.output
    classifier = 'mc1.15.1'
}

// Unit tests require native libraries to be available, which ForgeGradle
// doesn't do automatically...
test {
    dependsOn extractNatives
    systemProperty 'java.library.path', file('build/natives')
}
