buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = 'sponge'
            url = 'http://repo.spongepowered.org/maven'
        }
        jcenter()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}

apply plugin: 'net.minecraftforge.gradle.liteloader'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.jfrog.bintray'

version = "1.11a-beta1"
group = "com.uyjulian.LiteModWDL" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "LiteModWDL"

minecraft {
    version = "1.11"
    mappings = "snapshot_20161204"
    runDir = "run"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    filesSpec {
        from 'build/libs'
        into '.'
    }
    pkg {
        repo = 'MinecraftMods'
        name = 'LiteModWDL'
        version {
            name = project.version
            released = new Date()
        }
    }
}

sourceSets {
    main {
        // Refmap declaration must match the refmap name specified in the json config
        refMap = "mixins.LiteModWDL.refmap.json"
    }
}

mixin {
    defaultObfuscationEnv notch
}

litemod {
    json {
        name = "LiteModWDL"
        displayName = "World Downloader for Liteloader"
        author = "uyjulian"
        mcversion = "1.11"
        mixinConfigs += "mixins.LiteModWDL.json"
        version = project.version
    }
}

jar {
    from litemod.outputs
}
