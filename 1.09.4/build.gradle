buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        jcenter()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'pokechu22.test.begradle:begradle:0.2.3-SNAPSHOT:fg2'
    }
}

apply plugin: 'java'

version = "4.0.5.4-SNAPSHOT"
group = "wdl"
archivesBaseName = "WorldDownloader"
libsDirName = "../../libs/${version}"

apply plugin: 'pokechu22.test.begradle'
apply plugin: 'pokechu22.test.customsrg'
apply plugin: 'pokechu22.test.langrenamer'
apply plugin: 'pokechu22.test.noinplacereobf'

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:2.22.0'
    // Make sure that we have the Minecraft deps, at the correct versions
    testCompile configurations.forgeGradleMcDeps
}

minecraft {
    suppressMappingVersionWarnings = true
    version = "1.9.4"
    mappings = "snapshot_20190923"
    runDir = "run"

    // We don't currently want a source jar (doesn't work well for base edits, and the version info doesn't work yet)
    makeObfSourceJar = false
    // en_us.lang to en_US.lang
    ext.capitalizeLangFiles = true
    // Copy one language to its variants
    ext.langMap = [
        'es_es': ['es_ar', 'es_mx', 'es_uy', 'es_ve'],
        'fr_fr': ['fr_ca']
    ]
}

extraSrgs {
    addSrg file('../srgs/EntityHorseToEquineEntity.srg')
    addSrg file('../srgs/PositionedSound.srg')
    addSrg file('../srgs/AnimalChestToContainerHorseChest.srg')
    addSrg file('../srgs/Vec4bToMapDecoration.srg')
    addSrg file('../srgs/1.13.srg')
    addMethods file('../srgs/gamerule_methods_pre_1.14.3.csv')
    addMethods file('../srgs/pre_1.14_methods.csv')
    addMethods file('../srgs/1.12_methods.csv')
    addMethods file('../srgs/liteloader_1.9_methods.csv')
    addMethods file('../srgs/old_inventory_methods.csv')
    addMethods file('../srgs/1.11_gui_methods.csv')
    addFields file('../srgs/pre_1.14_fields.csv')
    addFields file('../srgs/1.12_fields.csv')
    addFields file('../srgs/old_inventory_fields.csv')
    addPatch('net.minecraft.entity.passive.EquineEntity.java', file('../srgs/EE_1.9.4.patch'))
}

sourceSets {
    main {
        java {
            srcDir "src/main/java"
            srcDir project(':share').file("src/main/java")
            exclude "wdl/StandardEntityManagers_1_12.java"
            exclude "wdl/StandardEntityManagers_1_13.java"
            exclude "wdl/StandardEntityManagers_1_13_1.java"
            exclude "wdl/WDLChunkLoader_1_13.java"
            exclude "wdl/handler/block/ShulkerBoxHandler.java"
            exclude "wdl/handler/block/BaseLargeChestHandler_1_13.java"
            exclude "wdl/handler/block/TrappedChestHandler.java"
            exclude "wdl/versioned/GameRules_1_13.java"
            exclude "wdl/versioned/Generator_1_13.java"
            exclude "wdl/versioned/Gui_1_12.java"
            exclude "wdl/versioned/Gui_1_13.java"
            exclude "wdl/versioned/Handlers_1_9_2.java"
            exclude "wdl/versioned/Handlers_1_12.java"
            exclude "wdl/versioned/Handlers_1_13.java"
            exclude "wdl/versioned/Maps_1_13.java"
            exclude "wdl/versioned/Maps_1_13_1.java"
            exclude "wdl/versioned/Packets_1_13.java"
            exclude "wdl/versioned/Registries_1_13_1.java"
            exclude "wdl/versioned/Types_1_9_2.java"
            exclude "wdl/versioned/Types_1_13.java"
            exclude "wdl/versioned/Types_1_13_1.java"
            exclude "wdl/versioned/Types_1_13_2.java"
            exclude "wdl/gui/widget/ExtButton_1_12.java"
            exclude "wdl/gui/widget/ExtButton_1_13.java"
            exclude "wdl/gui/widget/ExtGuiList_1_12.java"
            exclude "wdl/gui/widget/ExtGuiList_1_13.java"
            exclude "wdl/gui/widget/ExtGuiScreen_1_12.java"
            exclude "wdl/gui/widget/ExtGuiScreen_1_13.java"
        }
        resources {
            srcDir project(':share').file("src/main/resources")
        }
        base {
            baseClasses = [
                // Main hooks
                "net.minecraft.client.gui.GuiIngameMenu",
                "net.minecraft.client.multiplayer.WorldClient",
                "net.minecraft.client.network.NetHandlerPlayClient",
                "net.minecraft.inventory.InventoryBasic",
                // Auxiliary hooks, only needed for the base edit
                "net.minecraft.crash.CrashReport",
                "net.minecraft.client.ClientBrandRetriever",
                "net.minecraft.client.resources.DefaultResourcePack"
            ]
        }
    }
    test {
        java {
            srcDir project(':share').file("src/test/java")
            exclude "wdl/MixinTest**.java"
            exclude "wdl/NoMixinTest_1_9.java"
            exclude "wdl/NoMixinTest_1_11.java"
            exclude "wdl/NoMixinTest_1_12.java"
            exclude "wdl/NoMixinTest_1_13.java"
            exclude "wdl/ExtWorld_1_12.java"
            exclude "wdl/ExtWorld_1_13.java"
            exclude "wdl/ExtWorld_1_13_1.java"
            exclude "wdl/handler/block/ShulkerBoxTest.java"
            exclude "wdl/handler/block/TrappedChestTest_1_13.java"
            exclude "wdl/lwts/WDLTestTweaker_1_9.java"
            exclude "wdl/lwts/WDLTestTweaker_1_10.java"
            exclude "wdl/lwts/WDLTestTweaker.java"
        }
    }
    api {
        java {
            srcDir project(':share').file("src/api/java")
        }
    }
}

// We want Java 8 where possible
tasks.withType(JavaCompile) { javaTask ->
    javaTask.sourceCompatibility = "1.8"
    javaTask.targetCompatibility = "1.8"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

// Use tabs instead of spaces
fixMcSources {
    astyleConfig = "../astyle.cfg"
}

// Ensure that the API code is in the jar
jar {
    from sourceSets.api.output
    classifier = 'mc1.09.4' // For sorting purposes
}
