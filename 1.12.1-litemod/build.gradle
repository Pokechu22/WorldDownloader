buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = 'sponge'
            url = 'http://repo.spongepowered.org/maven'
        }
        jcenter()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'commons-io:commons-io:2.5'
        classpath 'com.googlecode.java-diff-utils:diffutils:1.3.0'
        classpath 'pokechu22.test.begradle:begradle:0.1.4-SNAPSHOT@jar'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'java'

version = "4.0.3.1-SNAPSHOT"
group = "wdl"
archivesBaseName = "LiteModWDL"
libsDirName = "../../libs/${version}"

apply plugin: 'net.minecraftforge.gradle.liteloader'
apply plugin: 'pokechu22.test.customsrg'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'pokechu22.test.langrenamer'
apply plugin: 'pokechu22.test.noinplacereobf'

repositories {
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:2.8.47'
    testCompile 'org.spongepowered:lwts:1.0.0'
    // Make sure that we have the Minecraft deps, at the correct versions
    testCompile configurations.forgeGradleMcDeps
}

minecraft {
    version = "1.12.1"
    mappings = "snapshot_20180913"
    runDir = "run"

    // We don't currently want a source jar (doesn't work well for base edits, and the version info doesn't work yet)
    makeObfSourceJar = false
    // Copy one language to its variants
    ext.langMap = [
        'es_es': ['es_ar', 'es_mx', 'es_uy', 'es_ve'],
        'fr_fr': ['fr_ca']
    ]
}

extraSrgs {
    addSrg file('../srgs/AbstractHorseToEquineEntity.srg')
    addSrg file('../srgs/1.13.srg')
    addMethods file('../srgs/1.12_methods.csv')
    addFields file('../srgs/1.12_fields.csv')
}

sourceSets {
    main {
        java {
            srcDir "src/main/java"
            srcDir project(':1.12.1').file("src/main/java")
            srcDir project(':share').file("src/main/java")
            exclude "wdl/WDLChunkLoader_1_13.java"
            exclude "wdl/versioned/GameRules_1_13.java"
            exclude "wdl/versioned/Generator_1_13.java"
            exclude "wdl/versioned/Handlers_1_9_2.java"
            exclude "wdl/versioned/Handlers_1_10.java"
            exclude "wdl/versioned/Handlers_1_13.java"
            exclude "wdl/versioned/Packets_1_13.java"
            exclude "wdl/gui/widget/ExtButton_1_11.java"
            exclude "wdl/gui/widget/ExtButton_1_13.java"
            exclude "wdl/gui/widget/ExtGuiList_1_11.java"
            exclude "wdl/gui/widget/ExtGuiList_1_13.java"
            exclude "wdl/gui/widget/ExtGuiScreen_1_9.java"
            exclude "wdl/gui/widget/ExtGuiScreen_1_13.java"
        }
        resources {
            srcDir "src/main/resources"
            srcDir project(':share').file("src/main/resources")
        }
        // Refmap declaration must match the refmap name specified in the json config
        ext.refMap = "mixins.LiteModWDL.refmap.json"
    }
    test {
        java {
            srcDir "src/test/java"
            srcDir project(':share').file("src/test/java")
            exclude "wdl/handler/SimpleWorld_1.10.java"
            exclude "wdl/handler/SimpleWorld_1.13.java"
        }
    }
    api {
        java {
            srcDir project(':share').file("src/api/java")
        }
    }
}

mixin {
    defaultObfuscationEnv notch
}

litemod {
    json {
        name = "LiteModWDL"
        displayName = "World Downloader for Liteloader"
        author = "uyjulian, pokechu22"
        mcversion = "1.12.1"
        mixinConfigs += "mixins.LiteModWDL.json"
        version = project.version
    }
}

// Use tabs instead of spaces
fixMcSources {
    astyleConfig = "../astyle.cfg"
}

// We want Java 8 where possible
tasks.withType(JavaCompile) { javaTask ->
    javaTask.sourceCompatibility = "1.8"
    javaTask.targetCompatibility = "1.8"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

// Ensure that the API code and litemod JSON in the jar
jar {
    from sourceSets.api.output
    from litemod.outputs
}

test {
    systemProperty 'lwts.tweaker', 'wdl.lwts.WDLTestTweaker'

    // Run tests in a temporary directory
    workingDir = { test.temporaryDir }
}
